group 'com.tzutalin.exgrpc'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'com.google.protobuf'
apply plugin: 'idea'

sourceCompatibility = 1.8

buildscript {
    repositories {
        mavenCentral()

        // Add for grpc
        dependencies {
            classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.0'
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'

    // Add for grpc
    compile 'io.grpc:grpc-netty:1.4.0'
    compile 'io.grpc:grpc-protobuf:1.4.0'
    compile 'io.grpc:grpc-stub:1.4.0'
}

clean {
    delete protobuf.generatedFilesBaseDir
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.2.0"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.3.0'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }

    generatedFilesBaseDir = "$projectDir/src/generated"
}

idea {
    module {
        sourceDirs += file("${protobuf.generatedFilesBaseDir}/main/java");
        // If you have additional sourceSets and/or codegen plugins, add all of them
        sourceDirs += file("${protobuf.generatedFilesBaseDir}/main/grpc");
    }
}

sourceSets {
    main {
        proto {
            // In addition to the default 'src/main/proto'
            srcDir 'src/main/proto'
            // In addition to the default '**/*.proto' (use with caution).
            // Using an extension other than 'proto' is NOT recommended,
            // because when proto files are published along with class files, we can
            // only tell the type of a file from its extension.
            include '**/*.protodevel'
        }
    }
    test {
        proto {
            // In addition to the default 'src/test/proto'
            srcDir 'src/test/protocolbuffers'
        }
    }
}

task(runUploadClient, dependsOn: 'classes', type: JavaExec) {
    main = 'UploadFileClient'
    classpath = sourceSets.main.runtimeClasspath
}

task(runUploadServer, dependsOn: 'classes', type: JavaExec) {
    main = 'UploadFileServer'
    classpath = sourceSets.main.runtimeClasspath
}